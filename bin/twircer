#!/usr/bin/perl

use strict;
use warnings;
use Net::Twitter::Lite;
use POE qw<Component::IRC::State>;
use YAML::Tiny qw<LoadFile>;

# PERL IS UNREADABLE
# SHOULD NOT BE USED FOR NEW DEVELOPMENT
# FORGIVE ME GOD

# FIXME: Should $ENV{HOME} be used?
my $config_file = $ENV{HOME} . '/.twircer/config';

die "Unable to open config file.\n" unless -e $config_file;

# FIXME: SHOULD THIS BE DONE?
chmod 0600, $config_file;

my $config = LoadFile($config_file);

die "Please setup your configuration file!\n" if defined $config->{edited};

my $nick     = $config->{nick}     || die "Error: No nick defined.\n";
my $server   = $config->{server}   || die "Error: No server defined.\n";
my $channels = $config->{channels} || die "Error: No channels defined.\n";
my $username = $config->{username} || die "Error: No username defined.\n";
my $password = $config->{password} || die "Error: No password defined.\n";
my $trigger  = $config->{trigger}  || die "Error: No trigger defined.\n";

my ( $irc, $twitter );

POE::Session->create(
   package_states => [ 
       'main' => [qw(_start irc_001 irc_public)], 
   ]
);

$poe_kernel->run();
exit 0;

sub _start {
   $irc = POE::Component::IRC::State->spawn(
      nick     => $nick,
      username => $nick,
      server   => $server,
      debug    => 1,

      #        plugin_debug => 1,
      #        options => { trace => 0 },
   ) or die "Unable to spawn IRC session.\n";

   $twitter = Net::Twitter::Lite->new(
      username        => $username,
      password        => $password,
      traits          => ['API::REST'],
      useragent_class => 'LWP::UserAgent::POE',
      source          => '',
   );

   $irc->yield( register => 'all' );
   $irc->yield( connect  => {} );
}

sub irc_001 {
   for my $chan (@$channels) {
      $irc->yield( join => $chan );
   }
}

sub irc_public {
   my ( $who, $where, $msg ) = @_[ ARG0 .. ARG2 ];

   my $channel = $where->[0];

   if ( $msg eq '@followers' ) {

      # FIXME: Should $ENV{HOME} be used?
      my $logfile = $ENV{HOME} . '/public_html/followers';

      open my $fh, '>', $logfile or do {
         $irc->yield(
            privmsg => $channel => "Error: Unable to open $logfile" );
         return;
      };
      chmod 0644, $logfile;

      my $followers = _get_followers();
      my $amount    = @$followers;

      print $fh $_->{screen_name}, "\n" for @$followers;

      my $whoami = getpwuid($<);
      $irc->yield( privmsg => $channel =>
            "$amount followers. See http://www.redbrick.dcu.ie/~$whoami/followers"
      );
   }

   # Line doesn't contain trigger, ignore
   return unless $msg =~ /\b\Q$trigger\E\b/;

   # Assume 'foo: ' is addressing the nick foo, and ignore
   return if $msg =~ /^[^:]+: /;

   # Ignore messages that contain lowercase letters
   # Except lines that begin with '@'
   # This allows tweets to be addressed to other twats
   if ( $msg =~ /^@\w+ / or $msg !~ /[a-z]/ ) {
      if ( length $msg > 140 ) {
         my $error
            = 'Your message was too long: '
            . ( length $msg )
            . ' chars. ACCESS DENIED.';
         $irc->yield( privmsg => $channel, $error );
         return;
      }
      eval { $twitter->update($msg) };

      # FIXME: Print proper error message?
      if ($@) {
         $irc->yield(
            privmsg => $channel,
            'ACHTUNG!!! I was unable to tweet!'
         );
      }
   }
}

sub _get_followers {

   # Example taken from Net::Twitter documentation
   # FIXME: Error checking?
   # FIXME: THIS TAKES LIKE 5 SECONDS, I WOULD LIKE IT TO BE INSTANT KTNX
   my @followers;

   for ( my $cursor = -1, my $r; $cursor; $cursor = $r->{next_cursor} ) {
      $r = $twitter->followers( { cursor => $cursor } );
      push @followers, @{ $r->{users} };
   }
   return \@followers;
}
